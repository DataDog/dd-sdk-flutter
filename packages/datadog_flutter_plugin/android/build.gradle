/*
 * Unless explicitly stated otherwise all files in this repository are licensed under the Apache License Version 2.0.
 * This product includes software developed at Datadog (https://www.datadoghq.com/).
 * Copyright 2016-Present Datadog, Inc.
 */


group "com.datadoghq.flutter"
version "1.0-SNAPSHOT"

buildscript {
    ext.kotlin_version = "1.8.22"
    ext.datadog_version = "2+"

    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:8.1.4"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

rootProject.allprojects {
    repositories {
        google()
        mavenCentral()
        maven {
             url("https://jitpack.io")
             content {
                includeGroup "com.github.xgouchet.Elmyr"
             }
        }
        maven {
            url "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
}

apply plugin: "com.android.library"
apply plugin: "kotlin-android"

android {
    namespace "com.datadoghq.flutter"
    compileSdkVersion 34

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    sourceSets {
        main.java.srcDirs += "src/main/kotlin"
    }

    defaultConfig {
        minSdkVersion 21
        multiDexEnabled true
    }
}

dependencies {
    implementation "com.datadoghq:dd-sdk-android-rum:$datadog_version"
    implementation "com.datadoghq:dd-sdk-android-logs:$datadog_version"
    implementation "com.datadoghq:dd-sdk-android-ndk:$datadog_version"

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "com.google.code.gson:gson:2.8.8"

    testImplementation(platform("org.junit:junit-bom:5.8.2"))
    testImplementation "org.junit.jupiter:junit-jupiter"
    testImplementation "org.mockito:mockito-junit-jupiter:4.3.1"
    testImplementation "com.github.xgouchet.Elmyr:core:1.3.1"
    testImplementation "com.github.xgouchet.Elmyr:junit5:1.3.1"
    testImplementation "org.mockito.kotlin:mockito-kotlin:4.0.0"
    testImplementation "io.mockk:mockk:1.13.7"
    testImplementation "com.willowtreeapps.assertk:assertk-jvm:0.25"
}

tasks.withType(Test) {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}
