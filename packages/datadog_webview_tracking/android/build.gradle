group 'com.datadoghq.flutter.webview'
version '1.0-SNAPSHOT'

buildscript {
    ext.kotlin_version = '1.8.22'
    ext.datadog_version = "2.12.1"

    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:8.1.4'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

android {
    namespace "com.datadoghq.flutter.webview"
    compileSdkVersion 34

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    defaultConfig {
        minSdkVersion 21
        multiDexEnabled true
    }
}

dependencies {
    implementation "com.datadoghq:dd-sdk-android-webview:$datadog_version"

    implementation project(":webview_flutter_android")

    testImplementation(platform("org.junit:junit-bom:5.8.2"))
    testImplementation "org.junit.jupiter:junit-jupiter"
    testImplementation "com.github.xgouchet.Elmyr:core:1.3.1"
    testImplementation "com.github.xgouchet.Elmyr:junit5:1.3.1"
    testImplementation "io.mockk:mockk:1.12.4"
    testImplementation "com.willowtreeapps.assertk:assertk-jvm:0.25"
}

tasks.withType(Test) {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}
